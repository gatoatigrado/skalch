// -*- coding: utf-8 -*-
// author: gatoatigrado (nicholas tung) [ntung at ntung]

// Licensed under the Apache License, Version 2.0 (the "License"); you may
// not use this file except in compliance with the License. You may obtain a
// copy of the License at http://www.apache.org/licenses/LICENSE-2.0 .

// determine which classes can be instantiated by ?? or !!, and
// write the code to do so.

/// values which have SKETCH annotations, e.g. @ Range
rule valueConstructAssigned {
    val:ValDef -:ValDefSymbol-> :Symbol
        -:SymbolAnnotation-> :SketchAnnotation;
    negative { val -:CanBeConstructAssigned-> val; }
    modify { val -:CanBeConstructAssigned-> val; }
}

/// values whose type are classes that are construct assignable
rule valueConstructAssigned2 {
    val:ValDef -:ValDefSymbol-> valsym:Symbol;
    valsym -:TypeSymbol-> typesym:Symbol;
    clsdef:ClassDef -:ClassDefSymbol-> typesym;
    clsdef -:CanBeConstructAssigned-> clsdef;
    negative { val -:CanBeConstructAssigned-> val; }
    modify { val -:CanBeConstructAssigned-> val; }
}

/// booleans have an implicit @Range({ true, false })
rule valueConstructAssigned3 {
    val:ValDef -:ValDefSymbol-> valsym:Symbol
        -:TypeSymbol-> typesym:Symbol;
    if { typesym.fullSymbolName == "scala.Boolean"; }
    negative { val -:CanBeConstructAssigned-> val; }
    modify { val -:CanBeConstructAssigned-> val; }
}

/// classes that have only construct assignable variables
/// and an $outer variable.
rule classConstructAssigned {
    cls:ClassDef -:ClassDefImpl-> tmpl:Template;
    negative {
        tmpl -:TemplateElement-> val:ValDef;
        negative { val -:CanBeConstructAssigned-> val; }
        negative { val -:ValDefSymbol-> :OuterSymbol; }
    }
    negative { cls -:CanBeConstructAssigned-> cls; }
    modify { cls -:CanBeConstructAssigned-> cls; }
}
