// -*- coding: utf-8 -*-
// author: gatoatigrado (nicholas tung) [ntung at ntung]

// Licensed under the Apache License, Version 2.0 (the "License"); you may
// not use this file except in compliance with the License. You may obtain a
// copy of the License at http://www.apache.org/licenses/LICENSE-2.0 .

// $then variable translation to function parameters

rule transformThisRef(fcn:FcnDef) {
    ths:This -:EnclosingFcn-> fcn;
    ths -thssymlink:ThisSymbol-> clssym:Symbol;
    fcn -:FcnDefParamsChain-> :ValDef -:ValDefSymbol-> thssym:Symbol;
    modify {
        thsref:ThisVarRef<ths> -:VarRefSymbol-> thssym;
        delete(thssymlink);
        eval {
            thsref.startLine = fcn.startLine;
            thsref.startCol = fcn.startCol;
            thsref.endLine = fcn.endLine;
            thsref.endCol = fcn.endCol;
        }
    }
}

rule addThisParamToEmpty(fcn:FcnDef, clssym:Symbol) {
    fcn -:FcnDefParamsChain-> first:EmptyChain; // NOTE -- different than below
    replace {
        thsparam:ThisValDef;
        thssym:Symbol;
        fcn2:MemberFcn<fcn>;
        eval {
            thsparam.scalaSource = "artificially inserted $this variable";
            thssym.symbolName = "this";
            thssym.fullSymbolName = "artificallyinserted.this";

            thsparam.startLine = fcn.startLine;
            thsparam.startCol = fcn.startCol;
            thsparam.endLine = fcn.endLine;
            thsparam.endCol = fcn.endCol;
        }
        fcn2 -:FcnDefParamsChain-> thsparam;
        fcn2 -:FcnDefParamsLast-> thsparam; // NOTE -- different than below
        thsparam -:ValDefRhs-> :EmptyTree;
        thsparam -:ValDefSymbol -> thssym;
        thssym -:TypeSymbol-> clssym;
    }
}

rule addThisParam(fcn:FcnDef, clssym:Symbol) {
    fcn -:FcnDefParamsChain-> first:ScAstNode;
    replace {
        thsparam:ThisValDef;
        thssym:Symbol;
        fcn2:MemberFcn<fcn>;
        eval {
            thsparam.scalaSource = "artificially inserted $this variable";
            thssym.symbolName = "this";
            thssym.fullSymbolName = "artificallyinserted.this";

            thsparam.startLine = fcn.startLine;
            thsparam.startCol = fcn.startCol;
            thsparam.endLine = fcn.endLine;
            thsparam.endCol = fcn.endCol;
        }
        fcn2 -:FcnDefParamsChain-> thsparam;
        thsparam -:FcnDefParamsNext-> first;
        thsparam -:ValDefRhs-> :EmptyTree;
        thsparam -:ValDefSymbol -> thssym;
        thssym -:TypeSymbol-> clssym;
    }
}

rule transformThis(fcn:FcnDef) {
    fcn -:FcnDefSymbol-> fcnsym:Symbol -:SymbolOwner-> cls:Symbol;
    fcnsym -:ClsMethod-> fcnsym;
    :ClassDef -:ClassDefSymbol-> cls;
    modify {
        exec((addThisParamToEmpty(fcn, cls) || addThisParam(fcn, cls))
            && transformThisRef(fcn)* || assertFalse("transform this failed"));
    }
}

// wrapper for "[rule]" (apply to all matches) graph rewriting
rule transformFcnWrapper {
    fcn:FcnDef;
    modify { exec(transformThis(fcn)); }
}

test testNoThisNodes {
    negative {
        ths:This;
    }
    negative {
        . -:ThisSymbol-> .;
    }
}
